import groovy.swing.SwingBuilder
import org.aspectj.bridge.IMessage
import org.aspectj.bridge.MessageHandler
import org.aspectj.tools.ajc.Main

buildscript {
  repositories {
    jcenter()
    mavenLocal()
  }
  dependencies {
    classpath 'org.aspectj:aspectjtools:1.8.1'
    classpath 'com.fernandocejas:gandalf:0.1.0'
  }
}

apply plugin: 'com.android.application'
apply plugin: 'com.fernandocejas.gandalf'

android {
  compileSdkVersion 19
  buildToolsVersion "20.0.0"

  defaultConfig {
    applicationId "com.example.gandalf"
    minSdkVersion 15
    targetSdkVersion 19
    versionCode 1
    versionName "1.0"
  }

  packagingOptions {
    exclude 'LICENSE.txt'
    exclude 'META-INF/DEPENDENCIES'
    exclude 'META-INF/ASL2.0'
    exclude 'META-INF/NOTICE'
    exclude 'META-INF/LICENSE'
  }

  lintOptions {
    abortOnError false
    disable 'InvalidPackage' // Some libraries have issues with this
  }

  signingConfigs {
    release {
      storeFile file("../distribution/android10.keystore")
      storePassword = ""
      keyAlias = ""
      keyPassword = ""
    }
  }

  buildTypes {
    debug {
      runProguard false
      applicationIdSuffix ".debug"
      versionNameSuffix "-debug"
    }
    release {
      signingConfig signingConfigs.release
      runProguard false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
      zipAlign true
    }
  }
}

gradle.taskGraph.whenReady { taskGraph ->
  if (taskGraph.hasTask(assembleRelease)) {
    Console console = System.console()
    def storePassword = ""
    def keyAlias = ""
    def keyPassword = ""

    if (console != null) {
      console.printf "\n----------------------------"
      console.printf "\nSigning Android Release APK"
      console.printf "\n----------------------------"

      storePassword = console.readPassword("\nKey Store Password: ")
      keyAlias = console.readPassword("Key Alias: ")
      keyPassword = console.readPassword("Key Password: ")
    } else {
      new SwingBuilder().edt {
        dialog(modal: true, title: "Sign APK", alwaysOnTop: true,
            resizable: false, locationRelativeTo: null, pack: true, show: true) {
          vbox {
            label(text: "Key Store Password: ")
            inputStorePassword = passwordField()
            label(text: "Key Alias: ")
            inputKeyAlias = passwordField()
            label(text: "Key Password: ")
            inputKeyPassword = passwordField()
            button(defaultButton: true, text: 'OK', actionPerformed: {
              storePassword = inputStorePassword.password;
              keyAlias = inputKeyAlias.password;
              keyPassword = inputKeyPassword.password;
              dispose();
            })
          }
        }
      }
    }

    if (storePassword.toString().empty) {
      throw new InvalidUserDataException("EMPTY Store Password")
    }
    if (keyAlias.toString().empty) {
      throw new InvalidUserDataException("EMPTY Key Alias")
    }
    if (keyPassword.toString().empty) {
      throw new InvalidUserDataException("EMPTY Key Password")
    }

    android.signingConfigs.release.storePassword = storePassword.toString()
    android.signingConfigs.release.keyAlias = keyAlias.toString()
    android.signingConfigs.release.keyPassword = keyPassword.toString()
  }
}

dependencies {
  compile project(':gandalf-library')
  compile 'org.aspectj:aspectjrt:1.8.1'
}

final def log = project.logger
final def variants = project.android.applicationVariants
variants.all { variant ->
  if (!variant.buildType.isDebuggable()) {
    log.debug("Skipping non-debuggable build type '${variant.buildType.name}'.")
    return;
  }

  JavaCompile javaCompile = variant.javaCompile
  javaCompile.doLast {
    String[] args = ["-showWeaveInfo",
                     "-1.5",
                     "-inpath", javaCompile.destinationDir.toString(),
                     "-aspectpath", javaCompile.classpath.asPath,
                     "-d", javaCompile.destinationDir.toString(),
                     "-classpath", javaCompile.classpath.asPath,
                     "-bootclasspath", project.android.bootClasspath.join(File.pathSeparator)]
    log.debug "ajc args: " + Arrays.toString(args)

    MessageHandler handler = new MessageHandler(true);
    new Main().run(args, handler);
    for (IMessage message : handler.getMessages(null, true)) {
      switch (message.getKind()) {
        case IMessage.ABORT:
        case IMessage.ERROR:
        case IMessage.FAIL:
          log.error message.message, message.thrown
          break;
        case IMessage.WARNING:
          log.warn message.message, message.thrown
          break;
        case IMessage.INFO:
          log.info message.message, message.thrown
          break;
        case IMessage.DEBUG:
          log.debug message.message, message.thrown
          break;
      }
    }
  }
}
